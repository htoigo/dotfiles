#+TITLE: Emacs Configuration

#+AUTHOR: Harry H. Toigo
#+EMAIL: hhtoigo@gmail.com
#+OPTIONS: toc:nil num:nil

* Start with Sensible Defaults

Due to its long history, or perhaps just the eccentricity of its creators, Emacs
has some rather odd default behavior, out-of-the-box. Let's bring things into
the contemporary era.

First, we begin with some sensible defaults. These settings were compiled from
Harry R. Schwartz's [[https://github.com/hrs/sensible-defaults.el][sensible-defaults.el]], and also from Phil Hagelberg's (aka
Technomancy's) [[https://github.com/technomancy/better-defaults][better-defaults]] ELPA package. And Magnar Sveen's...

** TODO Set up my own sensible defaults

   Pick and choose among +HRS's+, +Technomancy's+, and Magnar Sveen's settings.

** Settings from Technomancy's better-defaults package

Enable ido mode, unless helm has been installed--in which case, presumably, one
would prefer helm over ido mode.

ido mode allows many operations (like buffer switching and file navigation) to
be enhanced with instant feedback on the completion options. If you like ido,
you might want to check out [[https://github.com/nonsequitur/smex][smex]] and [[https://github.com/scottjad/ido-hacks][ido-hacks]]. Sometimes when creating a new
file, you'll want temporarily to disable ido; this can be done with C-f. You may
also want to look at the variable ido-use-virtual-buffers.

#+begin_src emacs-lisp
  (unless (fboundp 'helm-mode)
    (ido-mode t)
    (setq ido-enable-flex-matching t))
#+end_src

Turn off the menu bar, toolbar and scroll bars to gain valuable screen space.
Now entering the zen realm of nothing-but-the-text... These settings have been
moved to the UI Preferences theme section below.

The =uniquify= library makes it so that when you visit two files with the same
name in different directories, the buffer names have the directory name
prepended to them instead of the silly =file<2>= names you get by default.

#+begin_src emacs-lisp
  (require 'uniquify)
  (setq uniquify-buffer-name-style 'forward)
#+end_src

The =saveplace= library saves the location of point when you kill a buffer and
returns to it the next time you visit the associated file.

#+begin_src emacs-lisp
  (require 'saveplace)
  (setq-default save-place t)
#+end_src

Visually indicate matching pairs of parentheses. Make this display snappier by
reducing the delay before highlighting a matching paren from 0.125 seconds to
0.0625 seconds.

#+begin_src emacs-lisp
  (show-paren-mode)
  (setq show-paren-delay 0.0625)
#+end_src

Change the default of =indent-tabs-mode= to nil. Indentation can insert tabs if
this is non-nil.

#+begin_src emacs-lisp
  (setq-default indent-tabs-mode nil)
#+end_src

M-z (by default bound to zap-to-char) is instead bound to the far more useful
zap-up-to-char.

First, let's experiment with zap-to-char.

*** TODO Change the zap-up-to-char key binding to zf and zd

If we want to change as above, then let's bind the key-chords zf and zd to
zap-up-to-char forward and backward, respectively.

#+begin_src emacs-lisp
  (autoload 'zap-up-to-char "misc"
    "Kill up to, but not including ARGth occurrence of CHAR." t)
;;  (global-set-key (kbd "M-z") 'zap-up-to-char)
#+end_src

Other settings changes:
  - Apropos commands perform more extensive searches than the default.
  - Under X, killing and yanking uses the X clipboard rather than just the
    primary selection.
  - When middle-clicking the mouse to yank from the clipboard, insert the text
    where point is, not where the mouse cursor is.
  - ~require-final-newline~ is set to avoid problems with crontabs, etc.
  - When you perform a problematic operation, flash the frame instead of ringing
    the terminal bell. (~visible-bell~ set to t.)
  - Setting ~load-prefer-newer~ prevents stale elisp bytecode from shadowing
    more up-to-date source files.
  - Ediff is set up to use the existing frame instead of creating a new one.
  - Backups are stored in a single directory inside ~user-emacs-directory~.
    (Usually ~~/.emacs.d~)

#+begin_src emacs-lisp
  (setq apropos-do-all t
        save-interprogram-paste-before-kill t
        mouse-yank-at-point t
        require-final-newline t
        visible-bell t
        load-prefer-newer t
        ediff-window-setup-function 'ediff-setup-windows-plain
        save-place-file (concat user-emacs-directory "places")
        backup-directory-alist `(("." . ,(concat user-emacs-directory
                                                 "backups"))))
#+end_src

A few key bindings are replaced with more powerful equivalents:
  - M-/ is =hippie-expand= instead of =dabbrev-expand=
  - C-x C-b is =ibuffer= instead of =list-buffers=
  - C-s and C-r are swapped with regex-aware incremental search functions

#+begin_src emacs-lisp
  (global-set-key (kbd "M-/") 'hippie-expand)
  (global-set-key (kbd "C-x C-b") 'ibuffer)

  (global-set-key (kbd "C-s") 'isearch-forward-regexp)
  (global-set-key (kbd "C-r") 'isearch-backward-regexp)
  (global-set-key (kbd "C-M-s") 'isearch-forward)
  (global-set-key (kbd "C-M-r") 'isearch-backward)
#+end_src

** Settings from Harry Schwartz's sensible-defaults.el

Increase garbage collection threshold.

Allow 20 MB (instead of the default, 800 KB) of memory to be allocated before
doing garbage collection. This means GC runs less often, which speeds up some
operations.

#+begin_src emacs-lisp
  (setq gc-cons-threshold 20000000)
#+end_src

Call delete-trailing-whitespace whenever a buffer is saved.

#+begin_src emacs-lisp
  (add-hook 'before-save-hook 'delete-trailing-whitespace)
#+end_src

Treat CamelCase sub-words as separate words in every programming mode.

#+begin_src emacs-lisp
  (add-hook 'prog-mode-hook 'subword-mode)
#+end_src

Make scripts executable.

When saving a file that starts with `#!', make it executable.

#+begin_src emacs-lisp
  (add-hook 'after-save-hook
            'executable-make-buffer-file-executable-if-script-p)
#+end_src

Do not assume that sentences should have two spaces after the period. That was
for manual typewriters. Putting two spaces after a period is utterly, inarguably
wrong. See this [[https:www.slate.com/articles/technology/technology/2011/01/space_invaders.html][article on Slate]].

#+begin_src emacs-lisp
  (setq sentence-end-double-space nil)
#+end_src

Offer to create parent directories on save.

When saving a file in a directory that does not exist, offer to create
(recursively) the file's parent directories.

#+begin_src emacs-lisp
  (add-hook 'before-save-hook
            (lambda ()
              (when buffer-file-name
                (let ((dir (file-name-directory buffer-file-name)))
                  (when (and (not (file-exists-p dir))
                             (y-or-n-p (format "Directory %s does not exist. Create it?" dir)))
                    (make-directory dir t))))))
#+end_src

Apply changes to highlighted region.

Turn on transient-mark-mode.

#+begin_src emacs-lisp
  (transient-mark-mode t)
#+end_src

Overwrite selected text.

If some text is selected, and you begin typing printable text, replace the
selected text with your typed text.

#+begin_src emacs-lisp
  (delete-selection-mode t)
#+end_src

Quiet startup.

Do not present the usual startup message/screen, or the initial documentation
message displayed in the =*scratch*= buffer at startup.

#+begin_src emacs-lisp
  (setq inhibit-startup-message t)
  (setq initial-scratch-message nil)
#+end_src

Make dired file sizes human-readable.

Print human-readable sizes (e.g., 324K 9.5M 2G) in dired buffer listings,
instead of the exact number of bytes. This is done by adding ~-h~ to the flags
dired passes to ~ls~ by default.

#+begin_src emacs-lisp
  (setq-default dired-listing-switches "-alh")
#+end_src


Shorten yes-or-no questions.

Allow abbreviated responses to yes/no questions.

#+begin_src emacs-lisp
  (defalias 'yes-or-no-p 'y-or-n-p)
#+end_src

Always highlight code.

Turn on syntax highlighting whenever possible.

#+begin_src emacs-lisp
  (global-font-lock-mode t)
#+end_src

Refresh buffers when files change.

When something changes a file, automatically refresh the buffer associated with
that file so they do not get out of sync.

#+begin_src emacs-lisp
  (global-auto-revert-mode t)
#+end_src

Set the default line length to 80.

#+begin_src emacs-lisp
  (setq-default fill-column 80)
#+end_src

I am not sure if I want to use HRS's ~comment-or-uncomment-region-or-line~
function, below. The ~comment-dwim~ function already bound to =M-;= seems pretty
good to me, upon first inspection. So, for now I am not enabling this.


Comment or uncomment a region by hitting M-;. If there is no active region, it
works on the current line.

  (defun sensible-defaults/comment-or-uncomment-region-or-line ()
    "Comments or uncomments the region or the current line if
  there's no active region."
    (interactive)
    (let (beg end)
      (if (region-active-p)
          (setq beg (region-beginning) end (region-end))
        (setq beg (line-beginning-position) end (line-end-position)))
      (comment-or-uncomment-region beg end)))

  (global-set-key (kbd "M-;")
                  'sensible-defaults/comment-or-uncomment-region-or-line)

** Settings from Magnar Sveen

* Escape from Novice Mode

Certain commands in Emacs which "can easily confuse the user" are disabled by
default. This is the so-called novice mode. We are not confused by these
commands. Run at full power, please!

#+begin_src emacs-lisp
(put 'downcase-region 'disabled nil)
(put 'upcase-region 'disabled nil)
(put 'narrow-to-region 'disabled nil)
(put 'dired-find-alternate-file 'disabled nil)
#+end_src

* Set up the Emacs load path

Additional directories from which to load files.

#+begin_src emacs-lisp
  (setq lisp-dir
        (expand-file-name "lisp" user-emacs-directory))
  (setq settings-dir
        (expand-file-name "settings" user-emacs-directory))
  (add-to-list 'load-path lisp-dir)
  (add-to-list 'load-path settings-dir)
#+end_src

* Global Top-Level Definitions

Here we define some utility functions. I have chosen to include these defuns
here, rather than in another file, to see if the capabilities of Org mode are
sufficient for keeping things organized, while at the same time having the
entire Emacs configuration together in one place, printable, exportable, etc. as
one.

#+begin_src emacs-lisp
(defun install-if-needed (package)
  "Install a package only if it's not already installed."
  (unless (package-installed-p package)
    (package-install package)))

(defun move-beginning-of-line-or-indent (arg)
  "Move point to first non-whitespace character or beginning of line.

Move point to the first non-whitespace character on this line.
If point is already there, move to the beginning of the line.
Effectively toggle between the first non-whitespace character and
the beginning of the line.

If ARG is not nil or 1, move forward ARG - 1 lines first.  If
point reaches the beginning or end of the buffer, stop there."
  (interactive "^p")
  (or arg (setq arg 1))

  ;; Move by lines first, if ARG is not 1
  (when (/= arg 1)
    (let ((line-move-visual nil))
      (forward-line (1- arg))))

  (let ((orig-point (point)))
    (back-to-indentation)
    (when (= orig-point (point))
      (move-beginning-of-line 1))))

(defun duplicate-current-line-or-region (arg)
  "Duplicates the current line or region ARG times.
If there's no region, the current line will be duplicated. However, if
there's a region, all lines that region covers will be duplicated."
  (interactive "p")
  (let (beg end (origin (point)))
    (if (and mark-active (> (point) (mark)))
        (exchange-point-and-mark))
    (setq beg (line-beginning-position))
    (if mark-active
        (exchange-point-and-mark))
    (setq end (line-end-position))
    (let ((region (buffer-substring-no-properties beg end)))
      (dotimes (i arg)
        (goto-char end)
        (newline)
        (insert region)
        (setq end (point)))
      (goto-char (+ origin (* (length region) arg) arg)))))

(defun rotate-windows (arg)
  "Rotate your windows; use the prefix argument to rotate the other direction"
  (interactive "P")
  (if (not (> (count-windows) 1))
      (message "You can't rotate a single window.")
    (let* ((rotate-times (prefix-numeric-value arg))
           (direction (if (or (< rotate-times 0) (equal arg '(4)))
                          'reverse
                        'identity)))
      (dotimes (_ (abs rotate-times))
        (dotimes (i (- (count-windows) 1))
          (let* ((w1 (elt (funcall direction (window-list)) i))
                 (w2 (elt (funcall direction (window-list)) (+ i 1)))
                 (b1 (window-buffer w1))
                 (b2 (window-buffer w2))
                 (s1 (window-start w1))
                 (s2 (window-start w2))
                 (p1 (window-point w1))
                 (p2 (window-point w2)))
            (set-window-buffer-start-and-point w1 b2 s2 p2)
            (set-window-buffer-start-and-point w2 b1 s1 p1)))))))
#+end_src

* Do we need to set up Package.el here?

By default, Emacs automatically loads all installed packages. This happens at
startup, *after* processing the init file. (Emacs does not load packages at
startup if invoked with the `-q' or `--no-init-file' options.) To disable
automatic package loading, change the variable `package-enable-at-startup' to
`nil'.

In some circumstances, you may want to load packages explicitly in your init
file (usually because some other code in your init file depends on a package).
In that case, your init file should call the function `package-initialize'. This
will automatically set `package-enable-at-startup' to `nil', to avoid loading
the packages again after processing the init file. Alternatively, you may choose
to completely inhibit package loading at startup, and invoke the command `M-x
package-initialize' to load your packages manually. It is up to you to ensure
that relevant user options, such as `package-load-list' (see below), are set up
prior to the `package-initialize' call.

For finer control over package loading, you can use the variable
`package-load-list', *but* it seems that =use-package= allows for even better
control than `package-load-list' would, right?

** Workaround for Emacs bug #34341

 This manifests itself as the prompt: Buffer *elpa.gnu.org:431* has a running
 process; kill it? It also produces an error: `Bad request' when trying to
 download the package auctex, preventing us from installing it.

 The bug report is at https://debbugs.gnu.org/34341. It apparently is due to the
 function url-retrieve-synchronously returning a blank buffer, when it should
 return a https / TLS response.

 The workaround is to disable GNU TLS version 1.3:

 #+begin_src emacs-lisp
 (setq gnutls-algoritm-priority "normal:-vers-tls1.3")
 #+end_src

** Package Archives

 We use GNU ELPA, Non-GNU ELPA, and MELPA Stable? .

 #+begin_src emacs-lisp
 (require 'package)
 (when (< emacs-major-version 24)
   ;; For important compatibility libraries like cl-lib
   (add-to-list 'package-archives
                '("gnu" . "https://elpa.gnu.org/packages/")))
 ;; MELPA
 ;; (add-to-list 'package-archives
 ;;              '("melpa" . "https://melpa.org/packages/") t)
 ;; See `package-archive-priorities` and `package-pinned-packages`.
 (add-to-list 'package-archives
              '("melpa-stable" . "https://stable.melpa.org/packages/") t)

 ;; Prefer packages from GNU and Non-GNU archives.
 ;; Archives not in the list have priority 0.
 (setq package-archive-priorities
       '(("gnu" . 2)
         ("nongnu" . 1)))

 (package-initialize)
 #+end_src

* Configure =use-package=

If =use-package= is not already installed, bootstrap it.

#+begin_src emacs-lisp
  (unless (package-installed-p 'use-package)
    (package-refresh-contents)
    (package-install 'use-package))
#+end_src

Declare that we will be using =use-package=.

~use-package.el~ is no longer needed at runtime, so (require 'use-package) can
be wrapped in an ~eval-when-compile~ form, to further reduce load time.

#+begin_src emacs-lisp
  (eval-when-compile
    ;; The following is not needed if use-package.el is in ~/.emacs.d
    ;; (add-to-list 'load-path "/path/to/use-package.el")
    (require 'use-package))
    (require 'bind-key)           ; if you use any :bind variant
;;  (require 'diminish)           ; if you use :diminish
#+end_src

Tell =use-package= to be more verbose. Also, ensure packages are installed; i.e.
if a package declared with use-package has not been installed already, go ahead
and install it via package.el. These settings were used by Harry Schwartz.

#+begin_src emacs-lisp
  (setq use-package-verbose t
        use-package-always-ensure t)
#+end_src

Always compile packages.

#+begin_src emacs-lisp
  (use-package auto-compile
    :config (auto-compile-on-load-mode))
#+end_src

* Personal Information

** Who am I? Where am I?

#+begin_src emacs-lisp
  (setq user-full-name "Harry H. Toigo"
        user-mail-address "hhtoigo@gmail.com"
        calendar-latitude 37.8
        calendar-longitude -122.3
        calendar-location-name "Alameda, CA")
#+end_src

* Trying Out Packages

Try out packages before permanently installing them.

#+begin_src emacs-lisp
  (use-package try)
#+end_src

* UI Preferences
** Column number

 Show the column number along with the row in the modeline.

 #+begin_src emacs-lisp
 (setq column-number-mode t)
 #+end_src

** Fill column indicator

 We used to use package fill-column-indicator.

 This is now built-in to Emacs as =display-fill-column-indicator-mode=.

** Use fancy lambdas

 Why not?

 #+begin_src emacs-lisp
 (global-prettify-symbols-mode t)
 #+end_src

** Theme and other appearance settings

We store custom theme files in a =themes= subdirectory under
=user-emacs-directory= to reduce clutter in =user-emacs-directory=.

#+begin_src emacs-lisp
  (setq custom-theme-directory "~/.emacs.d/themes")
#+end_src

We have installed the =spacemacs-theme= package and are trying that. It doesn't
seem to need a use-package declaration. We used the Custom Themes menu command
to set it.

We also want to hide the menu bar, tool bar, and scrollbars. We do this in the
special function =hht/apply-look-and-feel= because of the problem mentioned
below.

The following allows us to adjust the transparency of frames. Transparency
seems to work better with dark themes.

#+begin_src emacs-lisp
  (defun set-frame-transparency (value)
    "Sets the transparency of the frame. 0 is transparent, 100 is opaque."
    (interactive "nTransparency Value 0 - 100 opaque:")
    (set-frame-parameter (selected-frame) 'alpha value))
#+end_src

*Problem:* When emacs is started as a server via =systemctl --user=, some frame
appearance settings don't take effect--e.g. it still shows a vertical scrollbar
even though we disabled it. This doesn't happen when =emacs= is run from the
terminal.

To work around this, we put these settings in a function which we add to the
hooks that are called after window frames are setup.

#+begin_src emacs-lisp
  (defun hht/apply-look-and-feel ()
    "Apply look-and-feel settings to the current frame."
    (interactive)
    ; (load-theme 'solarized-light t)
    (set-frame-transparency 90)
    (menu-bar-mode -1)
    (when (fboundp 'tool-bar-mode)
      (tool-bar-mode -1))
    (when (fboundp 'scroll-bar-mode)
      (scroll-bar-mode -1))
    (when (fboundp 'horizontal-scroll-bar-mode)
      (horizontal-scroll-bar-mode -1)))
#+end_src

If this code is being evaluated by =emacs --daemon=, ensure that all frames are
themed and scrollbars etc. are set appropriatedly.

#+begin_src emacs-lisp
  (if (daemonp)
      (add-hook 'after-make-frame-functions
        (lambda (frame)
           (with-selected-frame frame (hht/apply-look-and-feel))))
    (hht/apply-look-and-feel))

  ; Do we also need to add our function to the window-setup-hook?
  ; It was in one guy's code.
  ; (add-hook 'window-setup-hook #'my-look-and-feel)
#+end_src


We *were* using Bozhidar Batsov's solarized color theme, installed via its MELPA
package, =solarized-theme=. We have uninstalled this package. Even after uninstalling
there are solarized light and dark themes in the custom themes menu.

#+begin_src emacs-lisp :eval no
;  (use-package solarized-theme
;    :custom
;    (solarized-distinct-fringe-background t)
;    ;; (solarized-high-contrast-mode-line t)
;    (solarized-use-more-italic t)
;    (x-underline-at-descent-line t)
;    :config
;    (add-to-list 'custom-enabled-themes 'solarized-light))
#+end_src

** Tweak Window Chrome

 The default frame title is not useful. Instead, let's bind it to the name of
 the current project:

 #+begin_src emacs-lisp
 ;; (setq frame-title-format '((:eval (projectile-project-name))))
 (setq-default default-frame-alist (quote ((width . 88) (height . 40))))
 #+end_src

* Helpful Feedback
** Use =which-key=

 =which-key= automatically displays the possible completions for a prefix key
 after a short delay. This is really useful in some modes and for some prefix
 keys with lots of following keys.

 #+begin_src emacs-lisp
 (use-package which-key
   :diminish
   :config (which-key-mode))
 #+end_src

 But there is a problem with paging for certain prefix keys.

 =which-key= normally uses =C-h= as its paging key, to page through following
 keys if there are too many to display at once. However, if the sequence of the
 prefix key followed by =C-h= is bound to something, then one will be unable to
 page through =which-key='s keys because you trigger the command.

 The =which-key= [[https://github.com/justbur/emacs-which-key#paging-options][README]] does specify two methods for fixing this. However,
 Method 2, which sets the =which-key-paging-prefixes= variable, did not work for
 me. So instead we use Method 1, in which =which-key-use-C-h-commands= is set to
 true (which it is by default) so that the =C-h= key is used for paging. But we
 need to make some adjustments for the cases where the prefix key followed by
 =C-h= is bound to something. I found that simply unsetting the keybinding
 allows =which-key= to use =C-h= for paging like it wants to.

 =Help-char= prefix:
 For us =<f1>= since we use =C-h= for backspace like in the terminal.
 =<f1> C-h= is likely bound to =help-for-help=, so we unset it:

 #+begin_src emacs-lisp
 (keymap-global-unset "<f1> C-h")
 #+end_src

 =<esc>= prefix:
 For us =<esc> C-h= aka =C-M-h= was bound to =mark-defun=. We don't want to
 lose that keybinding, so we move it to =C-M-y= and unset =<esc> C-h=:

 #+begin_src emacs-lisp
 (keymap-unset esc-map "C-h")
 (keymap-set esc-map "C-y" 'mark-defun)
 #+end_src

 We have not found any other prefix keys with issues yet.

* Making Emacs Smarter

** Ido

 Enable ido-everywhere mode.

 #+begin_src emacs-lisp
 (ido-everywhere)
 #+end_src

** Enhance M-x

The =smex= package is a M-x enhancement for Emacs. Built on top of Ido, it
provides a convenient interface to your most recently and most frequently used
commands, and to all the other commands, too.

Move the old M-x to =C-c C-c M-x=.

For speed and improved ergonomics, we will use also bind =C-x C-j= to =smex=.
Steve Yegge recomends using =C-x C-m=, but that is identical to =C-x RET=, which
is a prefix key sequence for other commands.

Also, we make things more forgiving by binding =C-c C-j= to the same command, in
case we accidentally hit =C-c= instead of =C-x=.

#+begin_src emacs-lisp
  (use-package smex
    :bind (("M-x" . smex)
           ("M-X" . smex-major-mode-commands)
           ("C-x C-j" . smex)
           ("C-c C-j" . smex)
           ("C-c C-c M-x" . execute-extended-command)))
#+end_src

** Key-chords

Whoa, the mind-exploding power of key chords!

#+begin_src emacs-lisp
  (use-package use-package-chords
    :config (key-chord-mode 1))
#+end_src

* Editing Settings

** Make new buffers start in Text mode instead of Fundamental mode

#+begin_src emacs-lisp
  (setq-default major-mode 'text-mode)
#+end_src

** Quickly visit our Emacs configuration

I futz around with my dotfiles a lot. This allows me quickly to open my Emacs
configuration file with =C-c e=.

#+begin_src emacs-lisp
  (defun hht/visit-emacs-config ()
    (interactive)
    (find-file "~/.emacs.d/configuration.org"))

  (global-set-key (kbd "C-c e") 'hht/visit-emacs-config)
#+end_src

** Autocompletion: Use =company-mode= everywhere

Completion starts automatically after you type a few letters.

#+begin_src emacs-lisp
  (use-package company
    :hook (after-init . global-company-mode))
#+end_src

** Save my location within a file

Using =save-place-mode= saves the location of point for every file I visit. If I
close the file or close the editor, then later re-open it, point will be at the
same location.

#+begin_src emacs-lisp
  (save-place-mode t)
#+end_src

** Always indent with spaces

Never use tabs. Tabs are the devil's whitespace.

#+begin_src emacs-lisp
  (setq-default indent-tabs-mode nil)
#+end_src

** Deleting

When deleting files, use the system's trash can.

Enable Delete Selection mode, so that typed text replaces the selection if the
selection is active. Otherwise, typed text is just inserted at point regardless
of any selection.

#+begin_src emacs-lisp
  (setq delete-by-moving-to-trash t)
  (delete-selection-mode)
#+end_src

** Backups

Make backups of files even when they are under version control.

#+begin_src emacs-lisp
  (setq vc-make-backup-files t)
#+end_src

* Moving Around
** Window Navigation and Adjustment

Window navigation.

Use Shift + /arrow/ keys to move among windows, accomplished via
~windmove-default-keybindings~.

Rotate windows with =C-c C-,=

#+begin_src emacs-lisp
  (windmove-default-keybindings)
  (global-set-key (kbd "C-c C-,") 'rotate-windows)
  ;; How to bind (kbd "C-c C-.") to 'rotate-windows with a C-u or negative arg?
#+end_src

** Jumping Around
*** Ace Jump Mode

Using =ace-jump-mode=, we can navigate almost directly to any position in view,
within three key presses!

=C-;= starts Ace jump *word* mode

Enter the first character of a word and type one of the highlighted keys
covering each match to move to that word.

With a prefix argument =C-u C-;= starts Ace Jump *character* mode, allowing you
to jump to characters within words.

Enter a character, and then type one of the highlighted keys covering each match
to move to that character position.

=C-u C-u C-;= starts Ace jump *line* mode.

Each non-empty line will be marked; type one of the highlighted keys to move to
that line.

#+begin_src emacs-lisp
  (use-package ace-jump-mode
    ;; :bind (("C-;" . ace-jump-mode)
    ;;        ("C-S-;" . ace-jump-mode-pop-mark))
    :chords (("jj" . ace-jump-word-mode)
             ("jk" . ace-jump-char-mode)
             ("jl" . ace-jump-line-mode)
             ("jp" . ace-jump-mode-pop-mark))
    :config (ace-jump-mode-enable-mark-sync))
#+end_src

*** Replicate Vim's `f' command for intra-line jumps

The =jump-char= package is not available in MELPA Stable, so we cannot use it
for now.

The =jump-char= package gives us an iy-go-to-char enhancement, and acts like the
`f' key command in Vim. For speed, we bind this to the key chords =df= (forward)
and =sd= (backward).

Alas, key chords do not seem to work when defining a keyboard macro. So we need
another key binding to use when we are defining a keyboard macro. We no longer
need =M-m= for ~back-to-indentation~, since we have set up =C-a= to toggle
between that and beginning of line, so we also bind this to =M-m= (forward) and
=M-M= (backward).

#+begin_src emacs-lisp :eval no
;  (use-package jump-char
;    :chords (("df" . jump-char-forward)
;             ("sd" . jump-char-backward))
;    :bind (("M-m" . jump-char-forward)
;           ("M-M" . jump-char-backward)))
#+end_src

** Scroll conservatively

When point moves outside the window, Emacs usually re-centers the point. This
changes things to scroll just enough to bring point back into the window.

#+begin_src emacs-lisp
  (setq scroll-conservatively 100)
#+end_src

* Key Bindings

** Global

We do not need to quit Emacs that easily (or accidentally!), so we change things
so that =C-x C-c= does not quit Emacs, but rather closes the current frame.
Instead we quit Emacs with =C-x r q=. A mnemonic for this new way to quit Emacs
is C-x /really quit/.

#+begin_src emacs-lisp
  (global-set-key (kbd "C-x C-c") 'delete-frame)
  (global-set-key (kbd "C-x r q") 'save-buffers-kill-terminal)
#+end_src

Make creating a new frame easier. Use =C-x C-n= for that, instead of
~set-goal-column~ (used by ~next-line~ and ~previous-line~).

#+begin_src emacs-lisp
  (define-key global-map (kbd "C-x C-n") 'make-frame-command)
#+end_src

Bind =C-h= to backspace, same as in the shell. Use <f1> for help.

#+begin_src emacs-lisp
  (global-set-key (kbd "C-h") 'backward-delete-char-untabify)
#+end_src

Make =C-a= toggle between the first non-whitespace character on the line and the
beginning of the line. We have defined ~move-beginning-of-line-or-indent~ as a
utility function in the Global Top-Level Definitions section, above. Then we
remap ~move-beginning-of-line~ to ~move-beginning-of-line-or-indent~.

#+begin_src emacs-lisp
  (global-set-key [remap move-beginning-of-line]
                  'move-beginning-of-line-or-indent)
#+end_src

~duplicate-current-line-or-region~ is defined above in the Global Top-Level
Definitions section.

#+begin_src emacs-lisp
  (global-set-key (kbd "C-c d") 'duplicate-current-line-or-region)
#+end_src

* Email

** User Agent

=mu4e= is not packaged for Emacs =package.el=. There are =mu= and =mu4e=
packages in the openSUSE repos, and we have installed these via zypper.

Use mu4e as our mail user agent.

#+begin_src emacs-lisp
;;  (use-package mu4e
;;    :ensure nil
;;    :custom (mail-user-agent 'mu4e-user-agent)
;;            (mu4e-maildir "~/Maildir")        ; Fs path, no symlink.
;;                                              ; Other folders are relative to
;;                                              ; mu4e-maildir.
;;            (mu4e-sent-folder "/Sent Mail")   ; Removed the [Gmail] prefix here
;;            (mu4e-drafts-folder "/Drafts")    ; ditto
;;            (mu4e-trash-folder "/Trash"))     ; ditto
#+end_src

** Sending

Use smtpmail for sending email.

* Org Mode

Use prettier symbols than '***' for headlines:

#+begin_src emacs-lisp
(use-package org-bullets
  :hook (org-mode . org-bullets-mode))
#+end_src

Add timestamps for when entries are marked DONE:

#+begin_src emacs-lisp
(setq org-log-done 'time)
#+end_src

Expand the set of languages we can execute in code blocks from just emacs lisp:

#+begin_src emacs-lisp
(setq org-babel-load-languages '((emacs-lisp . t) (shell . t)))
#+end_src

* Software Development
** General

Install Projectile for navigating swiftly inside projects.

B. Batsov, in the Projectile README Quickstart section, recomends the following:

(projectile-mode +1)
(define-key projectile-mode-map (kbd "s-p") 'projectile-command-map)
(define-key projectile-mode-map (kbd "C-c p") 'projectile-command-map)

Harry Schwartz sets ~projectile-global-mode~, in order to use Projectile everywhere:

(projectile-global-mode)


  (use-package projectile
    :config
    (projectile-mode +1)
    (define-key projectile-mode-map (kbd "s-p") 'projectile-command-map)
    (define-key projectile-mode-map (kbd "C-c p") 'projectile-command-map))

** Python

 Switching from Elpy to using Eglot with a Python language server, Pylyzer and Ruff.

 ...

** Node/JavaScript

 We use =nvm= to manage node and npm versions. The =nvm= Emacs package provides
 a function ~nvm-use~ which allows us to call ~nvm use~ to adjust the exec-path
 to use a certain node version.

 #+begin_src emacs-lisp
 (use-package nvm
   :defer t)
 #+end_src

** Haskell

If we are using stack, then use FP Complete's intero haskell IDE mode.

#+begin_src emacs-lisp
  ;; (use-package intero
  ;;  :hook (haskell-mode . intero-mode))

  ;; (add-hook 'haskell-mode-hook #'intero-mode)
#+end_src

However, if we are not using stack, then we do not want intero loaded, as it
throws errors about ... , and we just end up disabling it anyway.

*** TODO Decide which mode to use for haskell editing without stack. haskell-mode?

How to detect that we are not using stack?

** Common Lisp

SLIME, the Superior Lisp Interaction Mode for Emacs.

#+begin_src emacs-lisp
  (use-package slime
    :config
    (setq inferior-lisp-program "/usr/bin/sbcl")
    (setq slime-contribs '(slime-fancy)))
#+end_src

** Clojure

clojure-mode is a major mode for editing Clojure and ClojureScript code.


  (use-package clojure-mode
    :hook (clojure-mode . #'subword-mode))


CIDER is a Clojure interactive development environment and REPL for Emacs.

  (use-package cider)

* Encryption

EasyPG: transparent, automatic gnupg encryption / decryption.

It appears that we may no longer need explicitly to do the following. I am leaving
it here for reference for now.

(use-package epa-file
  :config (epa-file-enable))

* TeX and LaTeX

AUCTeX makes Emacs into a great LaTeX editor. The AUCTeX manual is available
within Emacs via `F1 i d m AUCTeX RET'. The AUCTeX home page is at
www.gnu.org/software/auctex.

Make AUCTeX aware of style files and multi-file documents (using `\include' or
`\input').

Also load RefTeX, to make most important functions available for entering
RefTeX mode.

#+begin_src emacs-lisp
  (use-package tex
    :defer t
    :ensure auctex
    :config
    (setq TeX-auto-save t)
    (setq TeX-parse-self t)
    (setq-default TeX-master nil))

  (use-package reftex)
#+end_src
